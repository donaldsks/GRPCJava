apply plugin: 'application'
apply plugin: 'protobuf'

description = "gRPC Benchmarks"
mainClassName = "io.grpc.benchmarks.qps.Main"


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath libraries.protobuf_plugin
    }
}

configurations {
    alpnboot
}

dependencies {
    compile project(':grpc-core'),
            project(':grpc-netty'),
            project(':grpc-okhttp'),
            project(':grpc-stub'),
            project(':grpc-integration-testing'),
            libraries.junit,
            libraries.mockito,
            libraries.hdrhistogram

    // Determine the correct version of Jetty ALPN boot to use based
    // on the Java version.
    def alpnboot_prefix = 'org.mortbay.jetty.alpn:alpn-boot:'
    def alpnboot_version = '8.1.2.v20141202'
    if (JavaVersion.current().ordinal() < JavaVersion.VERSION_1_8.ordinal()) {
        alpnboot_version = '7.1.2.v20141202'
    }

    alpnboot alpnboot_prefix + alpnboot_version

    applicationDefaultJvmArgs = ["-Xbootclasspath/p:" + configurations.alpnboot.asPath]
}

protobufCodeGenPlugins = ["java_plugin:$rootDir/compiler/build/binaries/java_pluginExecutable/java_plugin"]
generateProto.dependsOn ':grpc-compiler:java_pluginExecutable'
